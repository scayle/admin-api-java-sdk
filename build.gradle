plugins {
    id 'java-library'
    id 'maven'
    id 'signing'
    id 'maven-publish'
}

repositories {
    jcenter()
    mavenCentral()
}

def getNexusUsername() {
    return hasProperty("nexusUsername") ? findProperty("nexusUsername") : ""
}

def getNexusPassword() {
    return hasProperty("nexusPassword") ? findProperty("nexusPassword") : ""
}

def isRelease() {
    findProperty("environment") == "release"
}

def resolveVersion() {
    return findProperty("version") + (isRelease() ? "" : "-SNAPSHOT")
}

group = 'com.scayle.adminapi'
version = resolveVersion()
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation 'com.google.guava:guava:32.0.1-jre'
    compile 'com.google.code.gson:gson:2.10.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:2.36.1'
    testImplementation 'com.squareup.moshi:moshi:1.14.0'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

    compile 'com.squareup.okhttp3:okhttp:4.10.0'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task fatJar(type: Jar) {
  archiveClassifier = "all"
  from {
    configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
  with jar
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    def signingKeyId = findProperty("signingKeyId")
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      pom.groupId = project.group
      pom.version = project.version

      repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: getNexusUsername(), password: getNexusPassword())
      }

      snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: getNexusUsername(), password: getNexusPassword())
      }

      pom.project {
        name 'SCAYLE AdminAPI SDK'
        packaging 'jar'
        description 'SCAYLE AdminAPI SDK'
        url 'https://scayle.dev/en/dev/admin-api/introduction'

        scm {
          connection 'scm:git:git@github.com:scayle/admin-api-java-sdk.git'
          developerConnection 'scm:git:git@github.com:scayle/admin-api-java-sdk.git'
          url 'https://github.com/scayle/admin-api-java-sdk'
        }

        licenses {
          license {
            name 'The MIT License'
            url 'https://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id 'SCAYLE'
            name 'SCAYLE Support'
            email 'support@scayle.com'
          }
        }
      }
    }
  }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}
